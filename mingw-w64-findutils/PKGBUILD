# Maintainer: Cyril Arnould <cyril.arnould@outlook.com>

_realname=findutils
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=4.10.0
pkgrel=1
pkgdesc="GNU utilities to locate files"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32' 'clangarm64')
license=('GPL3')
depends=("${MINGW_PACKAGE_PREFIX}-gettext"
         "${MINGW_PACKAGE_PREFIX}-libiconv"
         "${MINGW_PACKAGE_PREFIX}-libwinpthread-git")
makedepends=("${MINGW_PACKAGE_PREFIX}-autotools"
             "${MINGW_PACKAGE_PREFIX}-gcc")
checkdepends=("${MINGW_PACKAGE_PREFIX}-diffutils")
url="https://www.gnu.org/software/findutils"
source=("https://ftp.gnu.org/gnu/${_realname}/${_realname}-${pkgver}.tar.xz"{,.sig}
        "0001-Add-poll-and-windows-stat-inodes-gnulib-modules.patch"
        "0002-Skip-mountlist-check-on-mingw.patch"
        "0003-Replace-grp.h-and-pwd.h-with-functions-added-to-idca.patch"
        "0004-Replace-fork-with-spawnvp.patch"
        "0005-Modify-get_statinfo-to-handle-NTFS-symlinks.patch"
        "0006-Rename-CONTEXT_ALL.patch"
        "0007-Modify-tty_stream-names.patch"
        "0008-Fix-PATH-variable-parsing.patch"
        "0009-Fix-infinite-loops-in-do_time_format.patch"
        "0010-Avoid-MS-wildcard-globbing.patch"
        "0011-Define-missing-limits.patch"
        "0012-Skip-drop_privs-entirely-in-mingw-variant.patch"
        "0013-Require-HAVE_FCNTL-for-file-descriptor-leak-checks.patch"
        "0014-Require-signals-to-be-defined.patch")
sha256sums=('1387E0B67FF247D2ABDE998F90DFBF70C1491391A59DDFECB8AE698789F0A4F5'
            'SKIP'
            '1344E9561A005449DED177A9076A688809D2AFF434B574689182416E6FF730A6'
            '9A4F33A37AF113A736762FFC6925D69843031B50F2A450F9E2611986568F9235'
            'DDA9CB97DA4095A68840FB08FED3B824986FB9FDCC2A3C2A159C60444DA84C6F'
            '34889FB879413C3B697C92C5C8FC67FF060E583933C91EC1BEAC17629D6EF9DA'
            '2E560F037E25826FB19DE7A8C346888890F82F527FFE262C1585DD113921CE90'
            '7FBD3B837279C0C27C4742E0391C4650BE89C6B1381DAE5C95BF3C6B0D72AFD5'
            '4401DC618F4C4CF7BFC3637865818D327E1C54592BCE5E2C326ECE119CA074FA'
            '71063C31670BDB5ED5EEE4F8D4F8C6B0C5A720894AE908C48F38D4E9201DD499'
            'C74F0DA6F9253035F5F5BD3DBBCEC0666E07757BBA2646D46953AEFA19D48DBC'
            'DBE672F202C14A011B15603045AAEEEE31EFC92392BFC0F8FFAB9C175CC4E4BA'
            'C91F93DF2AD14BE65B6AA2AEE28D581186F08C96CF0ED97776B62C7D3EE6F000'
            '5BB1EA7B2FDB05A31ABC90D929F41253DE01652F90F789BCB4ED31A38628C594'
            '3BD5FA99A48537ACECC512FCA4794C4C14E4CD1835D7B3D9A6E17C855A45D409'
            'DAB446A1BA977DBD31AEB962A87A97AFBE28E08085F701CCF7C6AE4FC4B0AE8F')
validpgpkeys=('A5189DB69C1164D33002936646502EF796917195')

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"
  patch -Np1 -i "${srcdir}/0001-Add-poll-and-windows-stat-inodes-gnulib-modules.patch" # Manually modified to include the gnulib files
  patch -Np1 -i "${srcdir}/0002-Skip-mountlist-check-on-mingw.patch" # Manually modified to patch gl/m4/mountlist.m4
  patch -Np1 -i "${srcdir}/0003-Replace-grp.h-and-pwd.h-with-functions-added-to-idca.patch" # Manually modified to patch gl/lib/idcache.c
  patch -Np1 -i "${srcdir}/0004-Replace-fork-with-spawnvp.patch"
  patch -Np1 -i "${srcdir}/0005-Modify-get_statinfo-to-handle-NTFS-symlinks.patch"
  patch -Np1 -i "${srcdir}/0006-Rename-CONTEXT_ALL.patch"
  patch -Np1 -i "${srcdir}/0007-Modify-tty_stream-names.patch"
  patch -Np1 -i "${srcdir}/0008-Fix-PATH-variable-parsing.patch"
  patch -Np1 -i "${srcdir}/0009-Fix-infinite-loops-in-do_time_format.patch"
  patch -Np1 -i "${srcdir}/0010-Avoid-MS-wildcard-globbing.patch"
  patch -Np1 -i "${srcdir}/0011-Define-missing-limits.patch"
  patch -Np1 -i "${srcdir}/0012-Skip-drop_privs-entirely-in-mingw-variant.patch"
  patch -Np1 -i "${srcdir}/0013-Require-HAVE_FCNTL-for-file-descriptor-leak-checks.patch"
  patch -Np1 -i "${srcdir}/0014-Require-signals-to-be-defined.patch"
  autoreconf -fi
}

build() {
  cd "${srcdir}/${_realname}-${pkgver}"

  # Pacify GCC 14 errors
  CFLAGS+=" -Wno-error=implicit-function-declaration"
  CFLAGS+=" -Wno-error=incompatible-pointer-types"
  
  ./configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST}

  make
}

check() {
  cd "${srcdir}/${_realname}-${pkgver}"
  make check
}

package() {
  cd "${srcdir}/${_realname}-${pkgver}"
  make DESTDIR=${pkgdir} install
}
